buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE")
    }
}

version =  '0.1.0'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
}

dependencies {
    compile("org.springframework.data:spring-yarn-boot:2.2.0.RELEASE")
}

compileJava {
    sourceCompatibility=1.7
    targetCompatibility=1.7
}

task clientJar(type: Jar) {
    appendix = 'client'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/appmaster/*')
    exclude('**/container/*')
}

task appmasterJar(type: Jar) {
    appendix = 'appmaster'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/client/*')
    exclude('**/container/*')
}

task containerJar(type: Jar) {
    appendix = 'container'
    baseName = 'yarn-boot-simple'
    from sourceSets.main.output
    exclude('**/appmaster/*')
    exclude('**/client/*')
}

task clientBoot(type: BootRepackage, dependsOn: clientJar) {
    withJarTask = clientJar
    mainClass = 'hello.client.ClientApplication'
}

task appmasterBoot(type: BootRepackage, dependsOn: appmasterJar) {
    withJarTask = appmasterJar
    mainClass = 'hello.appmaster.AppmasterApplication'
}

task containerBoot(type: BootRepackage, dependsOn: containerJar) {
    withJarTask = containerJar
    mainClass = 'hello.container.ContainerApplication'
}

jar.enabled = false
bootRepackage.enabled = false

task bootJars
bootJars.dependsOn = [clientBoot, appmasterBoot, containerBoot]

build.dependsOn(bootJars)

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
